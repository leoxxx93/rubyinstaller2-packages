_realname=ruby
pkgbase=mingw-w64-ruby33
pkgname="${MINGW_PACKAGE_PREFIX}-ruby33"
# version used by MRI
pkgver2=3.3.8
# version used by pacman
pkgver="${pkgver2/-/.}"
pkgrel=1
pkgdesc="An object-oriented language for quick and easy programming (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'ucrt64')
url="https://www.ruby-lang.org/en"
license=("BSD, custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gmp"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-libyaml"
         "${MINGW_PACKAGE_PREFIX}-openssl>=3.0.0"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=("https://cache.ruby-lang.org/pub/ruby/${pkgver2%.*}/${_realname}-${pkgver2}.tar.xz"
        0001-Add-C-ext-win32-dll_directory-as-an-alternative-to-f.patch
        0002-Don-t-add-an-exe-file-manifest.patch
        0003-Use-autoconf-2.72.patch
        0004-Fix-C23-GCC-15-WIN32-compatibility-for-rb_define_-fu.patch
        0007-Fix-redefinition-of-clock_gettime-and-clock_getres.patch
        0009-Win32-Return-registry-strings-as-UTF-8.patch
        0010-Windows-Use-ATOMIC_PTR-functions-to-avoid-compiler-w.patch
        0011-ruby-io-console-Use-gperf-3.1-to-generate-ANSI-C-cod.patch
        ruby.ico
        rubyw.ico)

# To update SHA256 hashs run: ridk exec sh -c "updpkgsums PKGBUILD"

sha256sums=('44ae70fee043da3ce48289b7a52618ebe32dc083253993d486211c7e445c8642'
            'a50c81ab8c178689485257f8eaa70bd8184b878a798780084668f838912e2832'
            'da079dbfdc3d4e1b976ed15d58244ed6e0df201ec829bead578fb4a7177b9cab'
            'f26122cca5e289801c0dde1f54643a960246a69691c6c8b9bccb27e536344f69'
            'b1b509871ec2aef0fffcf50e5150382d5115a69ea59fac4a061643401689c88d'
            '972fd8114d2617cf1c0c3d7dae75d856bf5fa757daa9d0ebbdd17d21ddbb96e8'
            '5f84680f8cbce37b617f132e09009c111c78044d92378562456d8814db449346'
            '124f221ceb75d40e9f429603bf2383d8e60ad5fee9e2a9849e82bc9b89a41571'
            '6a4cc40328887f9ee20e4fb1b052c44d2c86e892b1f64a097d8763c2bd5cda25'
            '6c80ba2ca49840e387a08b7fedc6e7acd298ac2ec853155209efb2af20397b22'
            'c30fb04ac0e88ba634daaa811a81ea5a75027646617b895acd14df9518a4a55a')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver2}
  rm -rf ext/win32/dll_directory
  patch -p1 -i ${srcdir}/0001-Add-C-ext-win32-dll_directory-as-an-alternative-to-f.patch
  patch -p1 -i ${srcdir}/0002-Don-t-add-an-exe-file-manifest.patch
  patch -p1 -i ${srcdir}/0003-Use-autoconf-2.72.patch
  patch -p1 -i ${srcdir}/0004-Fix-C23-GCC-15-WIN32-compatibility-for-rb_define_-fu.patch
  patch -p1 -i ${srcdir}/0007-Fix-redefinition-of-clock_gettime-and-clock_getres.patch
  patch -p1 -i ${srcdir}/0009-Win32-Return-registry-strings-as-UTF-8.patch
  patch -p1 -i ${srcdir}/0010-Windows-Use-ATOMIC_PTR-functions-to-avoid-compiler-w.patch
  patch -p1 -i ${srcdir}/0011-ruby-io-console-Use-gperf-3.1-to-generate-ANSI-C-cod.patch
  autoreconf -fi
}

build() {

  CPPFLAGS+=" -DFD_SETSIZE=2048"
  CFLAGS="-O3 -fno-fast-math -fstack-protector-strong"
  LDFLAGS="-pipe -s -fstack-protector-strong"

  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  cp ${srcdir}/*.ico .

  ../${_realname}-${pkgver2}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-out-ext=readline,pty,syslog

  make showflags main
  echo "Generate RI and HTML documentation"
  # Make rdoc outputs so many lines -> print dots only
  make rdoc html 2>&1 | ./miniruby -ne "STDERR.print '.'"
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  echo $PATH
  make -l "TESTOPTS=-q" btest test-basic
  # TODO: Full tests fail currently
  # make -l "TESTOPTS=-q" test-all || true
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -f GNUMakefile DESTDIR="${pkgdir}" install
  for script in gem; do
    install ${srcdir}/ruby-${pkgver2}/bin/${script} \
      ${pkgdir}${MINGW_PREFIX}/bin/
  done
}
