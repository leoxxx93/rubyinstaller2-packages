_realname=ruby
pkgbase=mingw-w64-ruby-head
pkgname="${MINGW_PACKAGE_PREFIX}-ruby-head"
pkgver=r20250602
pkgver() {
  echo r`date +%Y%m%d`
}
pkgrel=1
pkgdesc="An object-oriented language for quick and easy programming (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clangarm64')
url="https://www.ruby-lang.org/en"
license=("BSD, custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             autoconf automake libtool patch make)
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gmp"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-libyaml"
         "${MINGW_PACKAGE_PREFIX}-openssl>=3.0.0"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git")
options=('staticlibs' 'strip')
source=("https://cache.ruby-lang.org/pub/ruby/snapshot/snapshot-master.tar.xz"
        0001-Add-C-ext-win32-dll_directory-as-an-alternative-to-f.patch
        0002-Use-autoconf-2.72.patch
        ruby.ico
        rubyw.ico
        placeholder.manifest.xml)
noextract=("snapshot-master.tar.xz")

# To update SHA256 hashs run: ridk exec sh -c "updpkgsums PKGBUILD"
# and set first entry to 'SKIP'

sha256sums=('SKIP'
            'a50c81ab8c178689485257f8eaa70bd8184b878a798780084668f838912e2832'
            'f26122cca5e289801c0dde1f54643a960246a69691c6c8b9bccb27e536344f69'
            '6c80ba2ca49840e387a08b7fedc6e7acd298ac2ec853155209efb2af20397b22'
            'c30fb04ac0e88ba634daaa811a81ea5a75027646617b895acd14df9518a4a55a'
            'c7a555d4826453efe7e33a2ebf78462b1fa665dffd7e4972143b48b1c9c71375')

prepare() {
  bsdtar -xf snapshot-master.tar.xz || true
  cd ${srcdir}/snapshot-master
  rm -rf ext/win32/dll_directory
  patch -p1 -i ${srcdir}/0001-Add-C-ext-win32-dll_directory-as-an-alternative-to-f.patch
  patch -p1 -i ${srcdir}/0002-Use-autoconf-2.72.patch
  autoreconf -fi
}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}
  CFLAGS="-O3 -fstack-protector-strong"
  PLACEHOLDER_DLL_RES=`cygpath -m ${srcdir}/build-${MSYSTEM}/placeholder-manifest-dll.res`
  LDFLAGS="-pipe -s -fstack-protector-strong ${PLACEHOLDER_DLL_RES} -lpthread"
  CXXFLAGS+=" -Wno-dll-attribute-on-redeclaration"

  cp ${srcdir}/*.ico .
  cp ${srcdir}/placeholder.manifest.xml .
  echo "2 24 placeholder.manifest.xml" > placeholder-manifest-dll.rc
  windres --input placeholder-manifest-dll.rc --output ${srcdir}/build-${MSYSTEM}/placeholder-manifest-dll.res --output-format=coff

  ../snapshot-master/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-out-ext=readline,pty,syslog \
    extra_warnflags="-Wno-shorten-64-to-32 -Wno-dll-attribute-on-redeclaration"

  make showflags main V=1

  echo "Generate RI and HTML documentation"
  # Make rdoc outputs so many lines -> print dots only
  make rdoc html 2>&1 | ./miniruby -ne "STDERR.print '.'"
}

check() {
  cd build-${MSYSTEM}
  echo $PATH
  make -l "TESTOPTS=-q" btest test-basic
  # TODO: Full tests fail currently
  # make -l "TESTOPTS=-q" test-all || true
}

package() {
  cd build-${MSYSTEM}
  make -f GNUmakefile DESTDIR="${pkgdir}" install
  for script in gem; do
    install ${srcdir}/snapshot-master/bin/${script} \
      ${pkgdir}${MINGW_PREFIX}/bin/
  done

  # Remove placeholder manifest from packaged rbconfig
  # The flags are inherited from the ruby build but not inteded for builds on the target system
  sed -i "s# ${PLACEHOLDER_DLL_RES}##g" ${pkgdir}/${MINGW_PREFIX}/lib/ruby/*/*/rbconfig.rb
}
